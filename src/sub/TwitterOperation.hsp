*TweetWindowProc
	oncmd 0

	switch iparam

		case WM_LBUTTONDOWN
			logmes "WM_LBUTTONDOWN"
			gosub *TwiWindowSelectImage
			swbreak

		case WM_MOUSEMOVE
			//うるさいのでコメントアウト
			//logmes "WM_MOUSEMOVE"
			gosub *RideCursor
			swbreak

		case WM_COMMAND
			logmes "WM_COMMAND"
			gosub *GetText
			swbreak

		case WM_DROPFILES
			logmes "WM_DROPFILES"
			gosub *OnDropFiles
			swbreak

		case WM_TWEET
			logmes "WM_TWEET"
			if (tweetFlg)^1{
				gosub *tweet_
			}
			swbreak

	swend

	oncmd 1
return


*TweetWindow

	if (ACCESS_TOKEN != "" & ACCESS_SECRET != ""){
		nidPush
		gsel WND_TWITTER
		gosub *TweetWindowDraw

		if (picHistoryCount == 0){
			sendmsg stackCounth ,$C , , strf("None")
		} else {
			sendmsg stackCounth ,$C , , strf("%3d/%3d", picHistoryNum+1, picHistoryCount)
		}
		nidPop
	}
return

*twiinit

	if ( ACCESS_TOKEN != "" && ACCESS_SECRET != "" ){

		nidPush

		setAccessToken ACCESS_TOKEN, ACCESS_SECRET

		gsel WND_TWITTER
		DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 1
		clrobj
		objmode 2
		title "ツイートウィンドウ"
		width Scale(448), Scale(262)

		font "メイリオ", Scale(12)

		pos 1,1
		SetTweetEditBox@TweetEditBoxMod tweetText, Scale(180), Scale(189), WM_TWEET, 1
		ttid = stat
		tth = objinfo(ttid,2)

		objsize Scale(55), Scale(17)

 		pos Scale(5), Scale(193)
 		chkbox "#タグ", addHashtag
 		addHashtagCId = stat
 		addHashtagCh = objinfo_hwnd(addHashtagCId)

 		pos Scale(60), Scale(191)
		input hashtag, Scale(120), Scale(19), 20
 		hashtagIId = stat
 		hashtagIh = objinfo_hwnd(hashtagIId)

		font "メイリオ", Scale(20)

		objsize Scale(140), Scale(32)
		pos 0, Scale(210)
		button gosub "ツイート",*tweet_
		tweetbid = stat

		objsize Scale(25), Scale(32)
		pos Scale(184), Scale(210)
		button gosub "<",*picHistoryTop

		objsize Scale(50), Scale(32)
		pos Scale(209), Scale(210)
		button gosub "←",*picHistoryBack
		picHistoryBackBId = stat
		objsize Scale(50), Scale(32)
		pos Scale(299), Scale(210)
		button gosub "→",*picHistoryNext
		picHistoryNextBId = stat

		objsize Scale(40), Scale(40)
		pos Scale(259), Scale(202)
		button gosub "↑", *picHistoryAddStack

		objsize Scale(32), Scale(40)

		pos Scale(350), Scale(202)
		button gosub "撮", *TwiCapture
		TwiWndCapBId = stat

		pos Scale(382), Scale(202)
		button gosub "「」", *TwiCaptureSelect
		twiCaptureSelectBId = stat

		pos Scale(414), Scale(202)
		button gosub "［］", *TwiCaptureSelectNonCut
		twiCaptureSelectNonCutBid = stat

		picHistoryNum = 0

		////スタティックテキスト
		font "メイリオ", Scale(14)
		mref BMSCR, 67
		hFont = BMSCR.38

		pos Scale(140), Scale(222)
		winobj "static", strf("%3d", 140-ttCount), , $50000002, Scale(44), Scale(20)
		ttCounth = objinfo(stat, 2)
		sendmsg ttCounth, $30, hFont

		pos Scale(379), Scale(181)
		winobj "static", strf("None"), , $50001002, Scale(65), Scale(20)
		stackCounth = objinfo(stat, 2)
		sendmsg stackCounth, $30, hFont

		sendmsg ttCounth, $C , , strf("%3d ", 140-ttCount-htCount)
		//----

		pos 0, Scale(242)
		winobj "msctls_progress32", "", , $50000000, Scale(448), Scale(20)
		hProgress = objinfo(stat, 2)
		sendmsg hProgress ,PBM_SETRANGE,0,MAKELPARAM(0,100)
		//----

		nidPop
		gosub *TweetWindow

	} else {

		nidPush

		gsel WND_TWITTER

		DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 0
		clrobj
		syscolor 15: boxf: color 8
		objmode 2
		width Scale(448), Scale(270)

		title "ツイートウィンドウ"
		font "メイリオ",Scale(14)
		pos Scale(10), Scale(10)
		mes "Twitterへの投稿には認証が必要です"
		mes "次のボタンを押すとブラウザが開くので表示されたコードを入力し、\nOKを押してください"

		pos Scale(10), Scale(80)
		objsize Scale(428), Scale(40)
		button gosub "ブラウザを開いて認証する",*openbrowser

		pos Scale(10), Scale(134)
		mes "PIN"
		PIN = ""
		pos Scale(40), Scale(130)
		input PIN, Scale(100), Scale(30)
		objsize Scale(150), Scale(30)
		pos Scale(150), Scale(130)
		button gosub "OK",*twioauthok

		pos 0,Scale(240)
		objsize Scale(448),Scale(30)
		button gosub "Amazonほしい物リスト", *Amazon: mesy+=30

		nidPop
	}

return

*picHistoryNext

	nidPush

	if (picHistoryNum < 99){

		if (picHistoryCount > picHistoryNum+1){
			picHistoryNum++
			gsel WND_TWIBUF
			if (ImgF_GetFormat(picHistory(picHistoryNum)) != 0){
				pos 0, 0: syscolor 15: boxf
				ImgF_PicloadEx picHistory(picHistoryNum), 1
			} else {
				pos 0, 0
				gcopy WND_NOIMAGE, 0, 0, BASE_SIZE_W, BASE_SIZE_H
			}
		}
	}

	nidPop
	gosub *tweetWindow
return

*picHistoryBack

	nidPush

	if (0 < picHistoryNum){
		picHistoryNum--
		gsel WND_TWIBUF
		if (ImgF_GetFormat(picHistory(picHistoryNum)) != 0){
			pos 0, 0: syscolor 15: boxf
			ImgF_PicloadEx picHistory(picHistoryNum), 1
		} else {
			pos 0, 0
			gcopy WND_NOIMAGE, 0, 0, BASE_SIZE_W, BASE_SIZE_H
		}
	}

	nidPop

	gosub *tweetWindow
return

*picHistoryTop

	nidPush

	picHistoryNum = 0
	gsel WND_TWIBUF
	if (ImgF_GetFormat(picHistory(picHistoryNum)) != 0){
		pos 0, 0: syscolor 15: boxf
		ImgF_PicloadEx picHistory(picHistoryNum), 1
	} else {
		pos 0, 0
		gcopy WND_NOIMAGE, 0, 0, BASE_SIZE_W, BASE_SIZE_H
	}

	nidPop

	gosub *tweetWindow
return

#deffunc picHistoryLoad str picHistoryLoadImageName
	nidPush

	picHistoryCount++
	if picHistoryCount > 100: picHistoryCount = 100

	i = picHistoryCount
	repeat picHistoryCount
		PicHistory(i) = PicHistory(i-1)
		i--
	loop
	PicHistory(0) = picHistoryLoadImageName

	gsel WND_TWIBUF
	if (ImgF_GetFormat(picHistory(picHistoryNum)) != 0){
		pos 0, 0: syscolor 15: boxf
		ImgF_PicloadEx picHistory(picHistoryNum), 1
	} else {
		pos 0, 0
		gcopy WND_NOIMAGE, 0, 0, BASE_SIZE_W, BASE_SIZE_H
	}

	nidPop
return

*picHistoryAddStack

	//画像履歴に表示されている画像が存在すれば
	if (ImgF_GetFormat(picHistory(picHistoryNum)) != 0){

		if (twiOrderCount == 4){
			repeat 4
				if (twiOrder(cnt) == 4){
					twiOrder(cnt) = 0
					twiOrderCount--
					break
				}
			loop
		}

		i = 3
		repeat 3
			if (twiOrder(i) = 0){
				PicStack(i) = PicStack(i-1)
				twiOrder(i) = twiOrder(i-1)
				twiOrder(i-1) = 0

				gsel WND_TWIBUF1+i
				pos 0, 0: syscolor 15: boxf
				gcopy WND_TWIBUF1+i-1, 0, 0, BASE_SIZE_W*ZOOM_MAX, BASE_SIZE_H*ZOOM_MAX
				twiBufPicSize(0, i) = twiBufPicSize(0, i-1), twiBufPicSize(1, i-1)

				gsel WND_TWIBUF_TMB1+i
				pos 0, 0: syscolor 15: boxf
				gcopy WND_TWIBUF_TMB1+i-1, 0, 0, 260*ZOOM_MAX, 156*ZOOM_MAX
			}
			i--
		loop
		PicStack(0) = picHistory(picHistoryNum)

		gsel WND_TEMP1
		ImgF_PicloadEx PicStack(0)
		ImgF_GetPicSize PicStack(0), twiBufPicSize(0, 0), twiBufPicSize(1, 0)

		gsel WND_TWIBUF1
		pos 0, 0: syscolor 15: boxf
		gcopy WND_TEMP1, 0, 0, BASE_SIZE_W*ZOOM_MAX, BASE_SIZE_H*ZOOM_MAX

		gsel WND_TWIBUF_TMB1
		pos 0, 0: color 200,200,200: boxf
		ImgP_CalcFitSize tDispW, tDispH, twiBufPicSize(0, 0), twiBufPicSize(1, 0), 260*ZOOM_MAX, 156*ZOOM_MAX
		pos (260*ZOOM_MAX-tDispW)/2, 0
		gzoom tDispW, tDispH, WND_TEMP1, 0, 0, twiBufPicSize(0, 0), twiBufPicSize(1, 0), 1

		if twiOrder(0) = 0{
			twiOrderCount++
			twiOrder(0) = twiOrderCount
		}

	}
	gosub *TweetWindow
return



*TwiCapture

	picHistoryNum = 0
	gosub *SSCapture
	if (stat): return

	if (seqCapf): picHistoryLoad savenamepath
	gosub *picHistoryAddStack

return


*TwiCaptureSelect

	marginCutFlg = TRUE

*TwiCaptureSelectNonCut

	nidPush

	dim selcap, 4
	picHistoryNum = 0

	SetLayeredWindowAttributes WND_INFO(WND_MAIN, WI_HANDLE), 0x00000000, 0, LWA_ALPHA
	SetLayeredWindowAttributes WND_INFO(WND_TWITTER, WI_HANDLE), 0x00000000, 0, LWA_ALPHA

	buffer WND_TEMP1, disinfo(2), disinfo(3)
	BitBlt hdc, 0,0, disinfo(2), disinfo(3), hdcScreen, disinfo(0), disinfo(1), SRCCOPY | CAPTUREBLT: redraw

	bgscr WND_TEMP_SCR1, disinfo(2), disinfo(3), 2: SET_WND_INFO
	font "メイリオ", 30

	gcopy WND_TEMP1, 0, 0, disinfo(2), disinfo(3)
	color 128,128,128
	repeat (disinfo(3) / 120)+1
		ycnt = cnt
		repeat (disinfo(2) / 500)+2
			pos (cnt*600)+(ycnt*70)-BASE_SIZE_W,ycnt*200
			mes "範囲選択キャプチャ"
		loop
	loop
	color 255,255,255
	boxf abs(disinfo(0)), abs(disinfo(1)), abs(disinfo(0))+970, abs(disinfo(1))+45
	color
	pos abs(disinfo(0))+5, abs(disinfo(1))+2
	mes "キャプチャ範囲をドラッグして下さい Escキーでキャンセル"

	dim selcap, 4

	SelectCapturePos WND_TEMP1, selcap, WND_OVERLAY, WND_TEMP_SCR1, marginCutFlg
	marginCutFlg = FALSE
	SetLayeredWindowAttributes WND_INFO(WND_MAIN, WI_HANDLE), 0x00000000, 255, LWA_ALPHA
	SetLayeredWindowAttributes WND_INFO(WND_TWITTER, WI_HANDLE), 0x00000000, 255, LWA_ALPHA

	selcapwh(0) = selcap(2)-selcap(0), selcap(3)-selcap(1)

	if ( selcapwh(0) > 4 && selcapwh(1) > 4){

		buffer WND_TEMP2, selcapwh(0), selcapwh(1)
		gcopy WND_TEMP1, selcap(0)+abs(disInfo(0)), selcap(1)+abs(disInfo(1)), selcapwh(0), selcapwh(1)

		nSaveName = strf("%s%s", Format@DateTimeFormatMod(saveName(SN_SELECTCAPTURE)), savekind(jpgsave))

		if (confirmSaveName(SN_SELECTCAPTURE)){
			SaveFileDialog saveNamePath, "画像ファイル(*.png)|*.png|画像ファイル( *.jpg)|*.jpg", jpgsave+1, nSaveName, selectCapSaveDir, strmid(savekind(jpgsave), 1, 3)
			if (stat == FALSE){
				logOut "キャンセルされました。"
				nidPop
				return 1
			}
			nSaveName = getpath@getpathMod(saveNamePath, 8)
		} else {
			saveNamePath = selectCapSaveDir +"\\"+ nSaveName
		}

		ImgF_ImageSave saveNamePath, jpgQuality

		gsel WND_MAIN

		if _exist(saveNamePath) <= 0 {
			successFlg = FALSE
			logOut nSaveName+"の保存に失敗しました"
		} else {
			successFlg = TRUE
			logOut nSaveName+"を保存しました"

			if dispcap & (mode == MODE_SSCAP){
				pos 0, dispcapy
				gzoom Scale(175), Scale(105), WND_TEMP2, 0, 0, selcapwh(0), selcapwh(1), 1
			}
			picHistoryLoad saveNamePath
			gosub *picHistoryAddStack

		}
	}

	nidPop
return

*TweetWindowDraw

	redraw 0
	gmode 0

	syscolor 15
	boxf
	syscolor 16 //外枠表示

	repeat 4
		boxf twiPicFrameXL(cnt),twiPicFrameYU(cnt),twiPicFrameXR(cnt),twiPicFrameYD(cnt)
	loop
	boxf twiPicFrameXL(4),twiPicFrameYU(4),twiPicFrameXR(4),twiPicFrameYD(4)

	syscolor 15 //内側上書き

	repeat 4
		boxf twiPicFrameXL(cnt)+1,twiPicFrameYU(cnt)+1,twiPicFrameXR(cnt)-1,twiPicFrameYD(cnt)-1
	loop
	boxf twiPicFrameXL(4)+2,twiPicFrameYU(4)+2,twiPicFrameXR(4)-2,twiPicFrameYD(4)-2
	color

	//ど真ん中の大きい所
	pos twiPicFrameXL(4)+2,twiPicFrameYU(4)+2
	gzoom twiPicFrameW(4)-3, twiPicFrameH(4)-3, WND_TWIBUF, 0, 0, BASE_SIZE_W, BASE_SIZE_H, 0

	repeat 4
		if PicStack(cnt) = "":break

		pos twiPicFrameXL(cnt)+1, twiPicFrameYU(cnt)+1
		gzoom Scale(62), Scale(38), WND_TWIBUF1+cnt, 0, 0, BASE_SIZE_W, BASE_SIZE_H
	loop

	gmode 2
	repeat 4
		if PicStack(cnt) = "": break
		pos twiPicFrameXL(cnt)+1, twiPicFrameYU(cnt)+1
		gcopy WND_IMAGE_BUF, 0, twiOrder(cnt)*120, Scale(62), Scale(38)
	loop

	gmode 0
	redraw 1

return

*rideCursor

	if ( ACCESS_TOKEN != "" && ACCESS_SECRET != "" ){

		mx = lparam&0x0000FFFF
		my = (lparam&0xFFFF0000)>>16
		rcFlg = -1

		repeat 4
			if PicStack(cnt) = "":break
			if ((twiPicFrameXL(cnt)<mx) & (twiPicFrameXR(cnt)>mx) & (twiPicFrameYU(cnt)<my) & (twiPicFrameYD(cnt)>my)){
				rcFlg = cnt
				break
			}
		loop

		if (rcFlg != rcFlg_) {

			logmes "選択が変わった"

			nidpush
			rcFlg_ = rcFlg

			if (rcFlg > -1){

				logmes "拡大表示"

				gsel WND_TWITTER

				color 200, 200, 200
				boxf Scale(180), 0, Scale(448), Scale(201)
				syscolor 8

				//詳細表示部
				pos twiPicFrameXL(4)+2, twiPicFrameYU(4)+2
				gzoom twiPicFrameW(4)-3, twiPicFrameH(4)-3, WND_TWIBUF_TMB1+rcFlg, 0, 0, 260*ZOOM_MAX, 156*ZOOM_MAX, 1

				//サムネイル部
				ImgP_CalcFitSize tDispW, tDispH, limit(twiBufPicSize(0, rcFlg), 0, Scale(BASE_SIZE_W)), limit(twiBufPicSize(1, rcFlg), 0, Scale(BASE_SIZE_H)), Scale(62), Scale(38)
				pos twiPicFrameXL(rcFlg)+1,twiPicFrameYU(rcFlg)+1
				gzoom tDispW, tDispH, WND_TWIBUF1+rcFlg, 0, 0, limit(twiBufPicSize(0, rcFlg), 0, Scale(BASE_SIZE_W)), limit(twiBufPicSize(1, rcFlg), 0, Scale(BASE_SIZE_H))

				sendmsg stackCounth ,$C , , strf("プレビュ")

			} else {

				logmes "元表示"

				gsel WND_TWITTER
				gosub *TweetWindowDraw
				if (picHistoryCount == 0){
					sendmsg stackCounth ,$C , , strf("None")
				} else {
					sendmsg stackCounth ,$C , , strf("%3d/%3d", picHistoryNum+1, picHistoryCount)
				}
			}
			nidpop
		}
	}
return

*TwiWindowSelectImage

	if (ginfo(24) == WND_TWITTER) {

		nidPush

		mx = lParam & 0x0000FFFF			// カーソルx座標
		my = (lParam >> 16) & 0x0000FFFF	// カーソルy座標

		gsel WND_TWITTER

		twiOrderPos = -1
		repeat 4
			if ((twiPicFrameXL(cnt)<mx) & (twiPicFrameXR(cnt)>mx) & (twiPicFrameYU(cnt)<my) & (twiPicFrameYD(cnt)>my)){
				twiOrderPos = cnt
			}
		loop

		if (twiOrderPos != -1){
			if (picStack(twiOrderPos) != ""){

				if twiOrder(twiOrderPos) = 0{
					twiOrderCount++
					twiOrder(twiOrderPos) = twiOrderCount
				} else {
					temp = twiOrder(twiOrderPos)
					twiOrder(twiOrderPos) = 0
					twiOrderCount--
					repeat 4
						if temp <= twiOrder(cnt):twiOrder(cnt)--
					loop
				}
				gosub *TweetWindow
			}
		}

		nidPop
	}


return

*tweet_

	if ( ACCESS_TOKEN != "" ){

		nidpush
		tweetFlg = TRUE

		gsel WND_MAIN
		gosub *SSModeControlDisable

		sdim mediaids, 1024
		sdim tempsdim, 1024, 4

		count = 0
		repeat 4
			if (twiOrder(cnt) == (count+1)) {
				if ( 2 <= ImgF_GetFormat(picStack(cnt)) ){
					tempsdim(count) = picStack(cnt)
					count++
					continue 0
				}
			}
		loop

		if (twijpg && jpgsave == FALSE){
			repeat count
				buffer WND_TEMP1
				//=========================================
				ImgF_PicloadEx tempsdim(cnt)
				saveNamed = strf("%s\\temp_%d.jpg", ssSaveDir, cnt)
				ImgF_ImageSave saveNamed, jpgQuality
				if _exist(saveNamed) > 0 {
					tempSdim(cnt) = saveNamed
				}
			loop
		}

		gsel WND_TWITTER

		mediaTweetSuccess = TRUE

		if (count != 0){
			media_upload tempSdim, count, mediaids, hProgress
			if (stat = 1){
			} else {
				mediaTweetSuccess = FALSE
				dialog "画像のアップロードに失敗しました"
			}
		}

		if (mediaTweetSuccess) {
			tempStr = tweettext
			if (addHashtag): tempStr+=" "+hashtag
			tweet tempStr, "", mediaids
			if (stat = 200){
				repeat 4
					picStack(cnt) = ""
				loop

				tweettext = ""
				tempStr = ""
				twiOrder = 0,0,0,0
				twiOrderCount = 0
				objprm ttid, tweettext
				sendmsg ttCounth, $C , , strf("%3d ", 140-htCount)
				sendmsg hProgress, $402, 0

				logmessage = "ツイートを投稿しました"

			} else {
				dialog "ツイートに失敗しました"
			}

		}

		if (twijpg == TRUE & jpgsave == FALSE){
			repeat count
				if _exist(tempsdim(cnt)) > 0 {
					delete tempsdim(cnt)
				}
			loop
		}

		gosub *SSModeControlEnable
		tweetFlg = FALSE
		LogOut logmessage
		nidPop
	}

	objsel ttid
	gosub *TweetWindow

return

*twireset
	//Twitterの認証情報をリセット
	userdata = ""
	ACCESS_TOKEN = ""
	ACCESS_SECRET = ""
	gosub *twiinit
return


*openbrowser

	address = getAuthorizeAddress()
	if (address != "Error"){
		exec "rundll32.exe url.dll,FileProtocolHandler \""+address+"\""
	} else {
		dialog "認証に失敗しました\nインターネットに繋がっているか確認しもう一度お試しください"
	}

return

*twioauthok

	userdata = ""
	ACCESS_TOKEN = ""
	ACCESS_SECRET = ""
	publishAccessToken ACCESS_TOKEN,ACCESS_SECRET,userdata,PIN

	if (stat != 200){
		dialog "Twitter認証に失敗しました\nもう一度お試しください"
	} else {
		gosub *twiinit
	}

return

*getText
	//ツイートウィンドウのテキストボックスに入力された文字数のカウント
	if (lparam = tth){
		if ( ((wparam >> 16)&0x0000FFFF) == 0x0400 ){
			ttCount = GetWindowText(tth, varptr(tweetTextBuf), 512) - SendMessage( tth, EM_GETLINECOUNT, 0, 0)+1
			sendmsg ttCounth, $C , , strf("%3d ", 140-ttCount-htCount)
		}
	}

	if (lparam == hashtagIh){
		if ( ((wparam >> 16)&0x0000FFFF) == 0x0400 ){
			if (addHashtag){
				htCount = GetWindowText(hashtagIh, varptr(tweetTextBuf), 512)+1
			} else {
				htCount = 0
			}
			sendmsg ttCounth ,$C , , strf("%3d",140-ttCount-htCount)
		}
	}

	if (lparam == addHashtagCh){
		sendmsg addHashtagCh, $F2
		addHashtag = stat & 0x01
		if (addHashtag){
			htCount = GetWindowText(hashtagIh, varptr(tweetTextBuf), 512)+1
		} else {
			htCount = 0
		}
		sendmsg ttCounth ,$C , , strf("%3d",140-ttCount-htCount)
 	}
return
